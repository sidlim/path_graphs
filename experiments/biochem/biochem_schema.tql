    define

# Also need a generic "molecule class" type
# I'm finding that a lot of these pathways given in FA don't specify exact reactants or exact products
# and will instead give molecule classes
# Example: in the arachidonic acid pathway, COXes (enzyme class) convert arachidonic acid into cyclic endoperoxides
#    Some ambiguity here: Cyclic endoperoxides might be a class of intermediates, or class of final products

# Another thing that I noticed: multi-activity enzymes exist, and I'm not sure if inhibitors act at the enzyme level
# or if they act at the reaction level - e.g., is lipocortin an inhibitor of the entire phospholipase A2 enzyme?
# or is it just an inhibitor of this particular reaction done by the enzyme?
# This definitely is a case-by-case situation, and I don't know what the right way to handle this is

# Part of the reason I haven't made much progress is scope. I'm not sure what should and shouldn't be part of the to-do list until the conferences
# One way to go might be to flesh out the biochemistry module
# MVP: a schema that lets us build and visualize hypergraphs

becomes sub relation,
    relates reactant,
    relates product,
    plays catalyzes:reaction;

catalyzes sub relation,
    relates reaction,
    relates catalyst,
    plays inhibits:catalysis;

inhibits sub relation,
    relates inhibitor,
    relates catalysis;

molecule sub entity,
    plays becomes:reactant,
    plays becomes:product,
    owns name @key;

enzyme sub molecule,
    plays catalyzes:catalyst;

name sub attribute, value string;

# Define two different kinds of changes here:
# One for enzyme inhibition processes
# And another for decreases in reactants or products

delta_quantity sub relation,
    relates quantity,
    relates origin,
    relates cause,
    owns direction;

delta_process sub relation,
    relates process,
    relates origin,
    relates cause,
    owns direction;

direction sub attribute, value long;

# Changes derived from changes in reactant quantity:
rule reactant-derived-delta: when {
    $reactant-change (quantity:$reactant, origin:$origin, direction:$d, cause:$_) isa delta_quantity;
    (reactant:$reactant, product:$product) isa becomes;
} then {
    (quantity:$product, origin:$origin, cause:$reactant-change, direction:$d) isa delta_quantity;
};

# Changes in product derived from reaction inhibition/activation:
rule reaction-derived-product-delta: when {
    $reaction (reactant:$reactant, product:$product) isa becomes;
    $reaction-change (process:$reaction, origin:$origin, direction:$d, cause:$_) isa delta_process;
} then {
    (quantity:$product, origin:$origin, direction:$d, cause:$reaction-change) isa delta_quantity;
};

# Changes in reactant derived from reaction inhibition/activation:
rule reaction-derived-reactant-delta: when {
    $reaction (reactant:$reactant, product:$product) isa becomes;
    $reaction-change (process:$reaction, origin:$origin, direction:$d, cause:$_) isa delta_process;
    # Flip the direction for the product side of the reaction change
    ?n = (-1) * $d;
} then {
    (quantity:$reactant, origin:$origin, direction:?n, cause:$reaction-change) isa delta_quantity;
};

# Changes derived from changes in enzyme quantity:
rule enzyme-derived-delta: when {
    $catalysis (reaction:$reaction, catalyst:$enzyme) isa catalyzes;
    $enzyme-change (quantity:$enzyme, origin:$origin, direction:$d, cause:$_) isa delta_quantity;
} then {
    (process:$reaction, origin:$origin, direction:$d, cause:$enzyme-change) isa delta_process;
};
